"Name"     = 'Nany'
"Author"   = 'Bruno BORRI, Damien GERARD'
"Version"  = '0.1'
"About"    = 'Grammar for the Nany scripting language, from the Yuni project team'
"Character Mapping" = 'Unicode'
"Case Sensitive" = 'True'
"Start Symbol" = <Program>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

{ID Head}      = {Letter} + {Letter Extended}
{ID Tail}      = {Alphanumeric} + [_]
{String Chars} = {Printable} + {HT} - ["\]
{Char Chars}   = {Printable} - ['']
{Hex Digit}    = {Digit} + [abcdef] + [ABCDEF]

! -------------------------------------------------
! Terminals
! -------------------------------------------------

Identifier    = {ID Head}{ID Tail}*
BooleanLiteral= 'true' | 'false'
StringLiteral = '"' ( {String Chars} | '\' {Printable} )* '"'
DecLiteral     = {Digit}+            ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?
HexLiteral     = '0'[xX]{Hex Digit}+ ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?
RealLiteral    = {Digit}*'.'{Digit}+ ( [FfDd] )?
CharLiteral    = '' ( {Char Chars} | '\'{Printable} )''
TimeLiteral    = ( {Digit}+ | {Digit}*'.'{Digit}+ ) ( 'ms' | 's' | 'm' | 'h' )
TypeQualifier  = 'const' | 'ref' | 'volatile'
VisibilityQualifier = 'public' | 'protected' | 'private' | 'published'
OptimQualifier = 'immutable' | 'stable' | 'threadunsafe'
BuiltInType    = 'any' | 'int' | 'unsigned int' | 'int8' | 'uint8' | 'int16' | 'uint16' | 'int32' | 'uint32' | 'int64' | 'uint64' | 'char' | 'float' | 'double' | 'bool'

! -------------------------------------------------
! Comments
! -------------------------------------------------

Comment Line = '#' | '//'
Comment Start = '#*' | '/*'
Comment End = '*#' | '*/'

! -------------------------------------------------
! Program / Unit
! -------------------------------------------------

! The grammar starts below
<Program> ::= <Unit Declaration> <Dependencies> <Implementation Block>

<Unit Declaration> ::= <Optional Visibility Qualifier> 'unit' Identifier ';'
               | 'program' Identifier ';'

<Dependencies> ::= <Dependency> <Dependencies>
               | ! Empty

<Dependency>   ::= 'uses' Identifier <Dependency Continued> ';'

<Dependency Continued> ::= '.' Identifier <Dependency Continued>
               | ! Empty

<Implementation Block> ::= <Function Declaration> <Implementation Block>
               | <Optional Visibility Qualifier> <Class Declaration> <Implementation Block>
               | <Optional Visibility Qualifier> <Workflow Declaration> <Implementation Block>
               | <Optional Visibility Qualifier> <Enum Declaration> <Implementation Block>
               | ! Empty

! -------------------------------------------------
! Literals
! -------------------------------------------------

<Literal> ::= BooleanLiteral
               |  DecLiteral
               |  HexLiteral
               |  RealLiteral
               |  TimeLiteral
               |  CharLiteral
               |  StringLiteral
               |  BuiltInType
               |  'nil'
               |  'self'

! -------------------------------------------------
! Classes
! -------------------------------------------------

<Class Declaration> ::= 'class' Identifier <Optional Type Parameters> <In Block> <Out Block> '{' <Class Content> '}'

! TODO
<Class Content> ::= VisibilityQualifier <Class Content>
               | <Method Declaration> <Class Content>
               | <Attribute Declaration> ';' <Class Content>
               | <Property Declaration> ';' <Class Content>
               | <Class Declaration> <Class Content> ! Nested class
               | ! Empty

<Property Declaration> ::= 'property' <Attribute Declaration> <Property Callbacks>

<Property Callbacks> ::= 'read' <Expression> <Property Callbacks>
               | 'write' <Expression> <Property Callbacks>
               | ! Empty

<Attribute Declaration> ::= Identifier ':' <Type Qualifiers> <Expression> <Optional Assignment>
               | Identifier ':=' <Expression>

<Optional Typing> ::= ':' <Type Qualifiers> <Expression>
               | ! Empty

<Optional Assignment> ::= ':=' <Expression>
               | ! Empty

! -------------------------------------------------
! Workflows
! -------------------------------------------------

<Workflow Declaration> ::= 'workflow' Identifier '{' <Workflow Content> '}'

<Workflow Content> ::= <State Block> <Transition Block>

<State Block> ::= 'states' <Workflow States>
               | ! Empty

<Workflow States> ::= 'default' Identifier ';' <Workflow States>
               | 'state' Identifier ';' <Workflow States>
               | ! Empty

<Transition Block> ::= 'transitions' <Workflow Transitions>
               | ! Empty

<Workflow Transitions> ::= 'default' 'allow' ';' <Workflow Transitions>
               | 'default' 'forbid' ';' <Workflow Transitions>
               | 'allow' <Workflow Permissions> '=>' <Workflow Permissions> ';' <Workflow Transitions>
               | 'forbid' <Workflow Permissions> '=>' <Workflow Permissions> ';' <Workflow Transitions>
               | ! Empty

<Workflow Permission> ::= '*'
               | Identifier
               | '+' Identifier
               | '-' Identifier

<Workflow Permissions> ::= <Workflow Permission> ',' <Workflow Permissions>
               | <Workflow Permission>

! -------------------------------------------------
! Enums
! -------------------------------------------------

<Enum Declaration> ::= 'enum' Identifier '{' <Enum Content> '}'

<Enum Content> ::= Identifier ',' <Enum Content>
               | Identifier
               | ! Empty


! -------------------------------------------------
! Functions and methods
! -------------------------------------------------

<Function Declaration> ::= <Optional Optim Qualifier> 'function' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <In Block> <Out Block> '{' <Statements> '}'

<Method Declaration> ::= <Optional Optim Qualifier> 'method' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <In Block> <Out Block> '{' <Statements> '}'

<Return Type Declaration> ::= ':' <Expression>
               | ! Empty

<Optional Parameters> ::= '(' <Parameter List> ')'
               | ! Empty

<Parameter List> ::= <Attribute Declaration> <Parameter List Continued>
               | Identifier <Parameter List Continued>
               | ! Empty

<Parameter List Continued> ::= ',' <Parameter List>
               | ! Empty

<Type Qualifiers> ::= TypeQualifier <Type Qualifiers Continued>
               | ! Empty

<Type Qualifiers Continued> ::= TypeQualifier <Type Qualifiers Continued>
               | ! Empty

<Optional Optim Qualifier> ::= OptimQualifier
               | ! Empty

<Optional Visibility Qualifier> ::= VisibilityQualifier
               | ! Empty

<Argument List> ::= <Expression> <Argument List Continued>
               | ! Empty

<Argument List Continued> ::= ',' <Expression>
               | ! Empty

! -------------------------------------------------
! Constraint blocks
! -------------------------------------------------

<In Block> ::= 'in' <Expression>
               | ! Empty

<Out Block> ::= 'out' <Expression>
               | ! Empty

! -------------------------------------------------
! Types
! -------------------------------------------------

<Optional Type Parameters> ::= '<' <Type Parameters>
               | ! Empty

! This is for generics declarations
<Type Parameters> ::= Identifier <Type Parameters Continued> '>'
               | Identifier ':=' <Expression> <Type Parameters Continued> '>'

<Type Parameters Continued> ::= ',' Identifier <Type Parameters Continued>
               | ',' Identifier ':=' <Expression> <Type Parameters Continued>
               | ! Empty

! This is for generics instanciations
<Type Arguments> ::= <Expression> <Type Arguments Continued> '>'

<Type Arguments Continued> ::= ',' <Expression> <Type Parameters Continued>
               | ! Empty

! -------------------------------------------------
! Expressions
! -------------------------------------------------

<Statements> ::= <Statement> <Statements Continued>
               | ! Empty

<Statements Continued> ::= <Statement> <Statements Continued>
               | ! Empty

<Statement>   ::= <Statement Expression> ';'
               | '&' <Statement Expression> ';'
               | 'return' <Expression> ';'
               | 'break' ';'
               | 'continue' ';'
               | <Attribute Declaration> ';'
               | 'if' <Expression> 'then' <Statement>
               | 'if' <Expression> 'then' <Statement> 'else' <Statement>
               | 'while' <Expression> 'do' <Statement>
               | 'for' Identifier ':=' <Expression> 'until' <Expression> 'do' <Statement>
               | 'for' Identifier ':=' <Expression> 'until' <Expression> 'step' <Expression> 'do' <Statement>
               | 'foreach' Identifier 'in' <Expression> 'do' <Statement>
               | 'foreach' Identifier 'in' <Expression> 'order' 'do' <Statement>
               | 'timeout' <Expression> 'do' <Statement>
               | 'timeout' <Expression> 'do' <Statement> 'else' <Statement>
               | '{' <Statements> '}'

<Assignment Exp> ::= <Expression> ':=' <Expression>

<Expression>  ::= 'if' <Expression> 'then' <Expression> 'else' <Expression>
               | <Statement Expression>

<Statement Expression> ::= <Expression> '|' <Xor Exp> ! Constraint
               |  <Xor Exp>

<Xor Exp>      ::= <Xor Exp> 'xor' <Or Exp> ! Boolean XOR
               |  <Or Exp>

<Or Exp>      ::= <Or Exp> 'or' <And Exp> ! Boolean Or
               |  <And Exp>

<And Exp>     ::= <And Exp> 'and' <Compare Exp> ! Boolean And
               |  <Equal Exp>

<Equal Exp>   ::= <Equal Exp> '='  <Compare Exp>    ! Equal
               |  <Equal Exp> '!=' <Compare Exp>    ! Not equal
               |  <Compare Exp>

<Compare Exp> ::= <Compare Exp> '>'  <Regexp Exp> 
               |  <Compare Exp> '<=' <Regexp Exp> 
               |  <Compare Exp> '>=' <Regexp Exp>
               |  <Inferior Exp>
               |  <Regexp Exp>

<Inferior Exp> ::= <Compare Exp> '<' <Regexp Exp>

! Is the priority correct ?
<Regexp Exp>  ::= <Regexp Exp> '~' <Shift Exp>
               |  <Shift Exp>

<Shift Exp>   ::= <Shift Exp> '<<' <Add Exp>
               |  <Shift Exp> '>>' <Add Exp>
               |  <Add Exp>

<Add Exp>     ::= <Add Exp> '+' <Mult Exp>
               |  <Add Exp> '-' <Mult Exp>
               |  <Mult Exp> 

<Mult Exp>    ::= <Mult Exp> '*' <Power Exp> 
               |  <Mult Exp> '/' <Power Exp> 
               |  <Power Exp>

<Power Exp>   ::= <Power Exp> '^' <Negate Exp>
               | <Negate Exp>

<Negate Exp>  ::= '-' <Value> 
               |  '--' <Value>
               |  '++' <Value>
               |  <Value> '--'
               |  <Value> '++'
               |  <Value>


! For use in arithmetic, requires parentheses around expressions to have proper priorities
! e.g. : weight := - (car.weight + passengers.weight)
<Value>  ::= <Simple Value>
               | <Simple Value> '.' <Subscript>

<Simple Value> ::= <Literal>
               | '(' <Expression> ')'
               | Identifier

<Subscript> ::= Identifier
               | Identifier '.' <Subscript> ! Property or attribute or function call with no argument
               | Identifier '(' <Argument List> ')' ! Function / Method call with arguments

