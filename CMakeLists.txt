PROJECT(TA3D)
#
# --- Settings ---
#
SET(CPACK_PACKAGE_VERSION_MAJOR "0")
SET(CPACK_PACKAGE_VERSION_MINOR "6")
SET(CPACK_PACKAGE_VERSION_PATCH "0")

SET(TA3D_VERSION_HI      "${CPACK_PACKAGE_VERSION_MAJOR}")
SET(TA3D_VERSION_LO      "${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
SET(TA3D_VERSION_TAG     "WIP")
SET(CPACK_PACKAGE_VERSION "${TA3D_VERSION_HI}.${TA3D_VERSION_LO}.${TA3D_VERSION_TAG}")

SET(TA3D_WEBSITE                "http://www.ta3d.org")
SET(TA3D_WEBSITE_FORUM          "http://www.ta3d.org/forums/")
SET(TA3D_MAIL_BUGREPORT         "zuzuf86@gmail.com")
SET(TA3D_WEBSITE_NEW_BUGREPORT  "http://trac.ta3d.org/newticket")

#
# Regression tests
#
SET(TA3D_REGRESSION_TEST 0)



#
# -----------------------------------------------------------------------------
#

SET(PACKAGE_BUGREPORT "${TA3D_MAIL_BUGREPORT}")
SET(PACKAGE_VERSION "${TA3D_VERSION_HI}.${TA3D_VERSION_LO} ${TA3D_VERSION_TAG}")
SET(PACKAGE_STRING "TA3D ${PACKAGE_VERSION}")
SET(TA3D_ENGINE_VERSION "${PACKAGE_STRING}")

# Getting the latest revision number
IF(WIN32 AND NOT CMAKE_CROSSCOMPILING)
    EXECUTE_PROCESS(COMMAND "subwcrev" "${PROJECT_SOURCE_DIR}" OUTPUT_VARIABLE SVN_REV)
    STRING(REGEX REPLACE ".* committed at revision ([0-9]+).*" "\\1" SVN_REV "${SVN_REV}")
ELSE(WIN32 AND NOT CMAKE_CROSSCOMPILING)
    EXECUTE_PROCESS(COMMAND "svnversion" "-n" "${PROJECT_SOURCE_DIR}" OUTPUT_VARIABLE SVN_REV)
    STRING(REGEX REPLACE "[0-9]+:" "" SVN_REV "${SVN_REV}")
    STRING(REPLACE "M" "" SVN_REV "${SVN_REV}")
ENDIF(WIN32 AND NOT CMAKE_CROSSCOMPILING)
MESSAGE(STATUS "TA3D -  v${TA3D_VERSION_HI}.${TA3D_VERSION_LO} (Rev: ${SVN_REV})")

IF(SVN_REV)
    SET(CPACK_PACKAGE_VERSION "${TA3D_VERSION_HI}.${TA3D_VERSION_LO}.${TA3D_VERSION_TAG}.r${SVN_REV}")
ENDIF(SVN_REV)

cmake_minimum_required(VERSION 2.6)
#SET(CMAKE_VERBOSE_MAKEFILE ON)
SET(LIBS "")


Message(STATUS "System: ${CMAKE_SYSTEM} (${CMAKE_SYSTEM_PROCESSOR})")


# We want -O2 -g -ffast-math (-pipe) as default options, but we want to be able to override CXX_FLAGS from commandline
string( LENGTH "${CMAKE_CXX_FLAGS}" VA )

IF( ${VA} EQUAL 0 )
    IF(WIN32)
	    SET( CMAKE_CXX_FLAGS "-O2 -g -ffast-math " )
    ELSE(WIN32)
	    SET( CMAKE_CXX_FLAGS "-O2 -g -Wall -ffast-math -pipe " )
    ENDIF(WIN32)
ENDIF( ${VA} EQUAL 0 )

message(STATUS "Default C++ flags set to `${CMAKE_CXX_FLAGS}`")



#
# DEBUG
#
MESSAGE(STATUS "[DEBUG] The Debug mode has been enabled")
ADD_DEFINITIONS("-DLOGS_USE_DEBUG")

#
# AutoTest
#
IF(${TA3D_REGRESSION_TEST} EQUAL 1)
    MESSAGE(STATUS "[DEBUG] The regression tests will be compiled")
ENDIF(${TA3D_REGRESSION_TEST} EQUAL 1)


#
# Platform
#
IF(UNIX AND NOT CMAKE_CROSSCOMPILING)
    IF(APPLE)
        ADD_DEFINITIONS("-DTA3D_PLATFORM_DARWIN -DTA3D_PLATFORM_MAC")
        SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fvisibility=hidden")
    ELSE(APPLE)
    	SET(LINUX 1)
        ADD_DEFINITIONS("-DTA3D_PLATFORM_LINUX")
    ENDIF(APPLE)
    ADD_DEFINITIONS("-DTA3D_PLATFORM_UNIX -DTA3D_PLATFORM_GCC")
ELSE(UNIX AND NOT CMAKE_CROSSCOMPILING)
    IF(WIN32)
        ADD_DEFINITIONS("-DTA3D_PLATFORM_WINDOWS -DTA3D_PLATFORM_WIN32")
    ELSE(WIN32)
        # Win64 ?
    ENDIF(WIN32)
ENDIF(UNIX AND NOT CMAKE_CROSSCOMPILING)


Include( CheckIncludeFile )


#
# OpenGL
#
Include(FindOpenGL)
SET(OPEN_PREFIX "[OpenGL]")
IF(NOT OPENGL_FOUND)
    message( FATAL_ERROR "${OPEN_PREFIX} Libraries not found !!" )
ENDIF(NOT OPENGL_FOUND)

# Includes
IF(NOT WIN32 AND NOT CMAKE_CROSSCOMPILING)
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -I${OPENGL_INCLUDE_DIR}")
ENDIF(NOT WIN32 AND NOT CMAKE_CROSSCOMPILING)

# Infos
MESSAGE(STATUS "${OPEN_PREFIX} Mesa: ${OPENGL_XMESA_FOUND}")
MESSAGE(STATUS "${OPEN_PREFIX} GLu: ${OPENGL_GLU_FOUND}")
MESSAGE(STATUS "${OPEN_PREFIX} Include dir: ${OPENGL_INCLUDE_DIR}")
MESSAGE(STATUS "${OPEN_PREFIX} The GL library: ${OPENGL_gl_LIBRARY}")
MESSAGE(STATUS "${OPEN_PREFIX} The GLU library: ${OPENGL_glu_LIBRARY}")

# Frameworks / Libraries
IF(APPLE)
    MESSAGE(STATUS "${OPEN_PREFIX} Added Frameworks: AGL, OpenGL, Carbon")
    LINK_LIBRARIES("-framework AGL -framework OpenGL -framework Carbon")
ELSE(APPLE)
    STRING(REPLACE ";" "\n      .  " OPENGL_LIBRAIRIES_MSG "${OPENGL_LIBRARIES}")
    MESSAGE(STATUS "${OPEN_PREFIX} Added Libraries :")
    MESSAGE("      .  ${OPENGL_LIBRAIRIES_MSG}")
    IF(WIN32 OR CMAKE_CROSSCOMPILING)
        LINK_LIBRARIES(-lopengl32 -lglu32)
    ELSE(WIN32 OR CMAKE_CROSSCOMPILING)    
        LINK_LIBRARIES(${OPENGL_LIBRARIES})
    ENDIF(WIN32 OR CMAKE_CROSSCOMPILING)
ENDIF(APPLE)



#
# SDL
#
Include(FindSDL)
Include(FindSDL_image)
Include(FindSDL_mixer)
Include(FindSDL_net)
SET(SDL_PREFIX "[SDL]")

# Manual setup for mingw32 (all required files are included in source tree)
IF (WIN32 OR CMAKE_CROSSCOMPILING)
	SET( SDL_FOUND included )
	SET( SDLIMAGE_FOUND included )
	SET( SDLMIXER_FOUND included )
	SET( SDLNET_FOUND included )

	INCLUDE_DIRECTORIES( "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/include" )

	SET( SDL_INCLUDE_DIR  "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/include/SDL")
	SET( SDLIMAGE_INCLUDE_DIR  "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/include/SDL")
	SET( SDLMIXER_INCLUDE_DIR  "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/include/SDL")
	SET( SDLNET_INCLUDE_DIR  "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/include/SDL")

	SET( SDL_LIBRARY "mingw32;${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/libs/libSDLmain.a;${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/libs/SDL.dll")
	SET( SDLIMAGE_LIBRARY "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/libs/SDL_image.dll")
	SET( SDLMIXER_LIBRARY "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/libs/SDL_mixer.dll")
	SET( SDLNET_LIBRARY "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/libs/SDL_net.dll")
ENDIF (WIN32 OR CMAKE_CROSSCOMPILING)

MESSAGE(STATUS "${SDL_PREFIX} SDL: ${SDL_FOUND}")
MESSAGE(STATUS "${SDL_PREFIX} SDL_image: ${SDLIMAGE_FOUND}")
MESSAGE(STATUS "${SDL_PREFIX} SDL_mixer: ${SDLMIXER_FOUND}")
MESSAGE(STATUS "${SDL_PREFIX} SDL_net: ${SDLNET_FOUND}")
MESSAGE(STATUS "${SDL_PREFIX} Include dir: ${SDL_INCLUDE_DIR}")
MESSAGE(STATUS "${SDL_PREFIX} The SDL library: ${SDL_LIBRARY}")
MESSAGE(STATUS "${SDL_PREFIX} The SDL_image library: ${SDLIMAGE_LIBRARY}")
MESSAGE(STATUS "${SDL_PREFIX} The SDL_mixer library: ${SDLMIXER_LIBRARY}")
MESSAGE(STATUS "${SDL_PREFIX} The SDL_net library: ${SDLNET_LIBRARY}")

IF(NOT SDL_FOUND)
    MESSAGE(FATAL_ERROR "SDL not found, SDL is required")
ENDIF(NOT SDL_FOUND)
IF(NOT SDLIMAGE_FOUND)
    MESSAGE(FATAL_ERROR "SDL_image not found, SDL_image is required")
ENDIF(NOT SDLIMAGE_FOUND)
IF(NOT SDLMIXER_FOUND)
    MESSAGE(FATAL_ERROR "SDL_mixer not found, SDL_mixer is required")
ENDIF(NOT SDLMIXER_FOUND)
IF(NOT SDLNET_FOUND)
    MESSAGE(FATAL_ERROR "SDL_net not found, SDL_net is required")
ENDIF(NOT SDLNET_FOUND)
LINK_LIBRARIES(${SDL_LIBRARY})
LINK_LIBRARIES(${SDLIMAGE_LIBRARY})
LINK_LIBRARIES(${SDLMIXER_LIBRARY})
LINK_LIBRARIES(${SDLNET_LIBRARY})

INCLUDE_DIRECTORIES(${SDL_INCLUDE_DIR})
INCLUDE_DIRECTORIES(${SDLIMAGE_INCLUDE_DIR})
INCLUDE_DIRECTORIES(${SDLMIXER_INCLUDE_DIR})
INCLUDE_DIRECTORIES(${SDLNET_INCLUDE_DIR})

link_directories( ${TA3D_SOURCE_DIR}/src/lua/ )


IF( WIN32 OR CMAKE_CROSSCOMPILING )
	IF( CMAKE_CROSSCOMPILING )
		SET( target i586-mingw32msvc- )
	    SET( MAKE_WIN "make")
	    SET( RANLIB_WIN "${target}ranlib")
	ELSE( CMAKE_CROSSCOMPILING )
	    FIND_PROGRAM(MAKE_WIN "make.exe")
	    FIND_PROGRAM(RANLIB_WIN "ranlib.exe")
	ENDIF( CMAKE_CROSSCOMPILING )

	IF( NOT BUILT_LUA )
		EXECUTE_PROCESS(WORKING_DIRECTORY "${TA3D_SOURCE_DIR}/src/lua" COMMAND ${MAKE_WIN} clean)
		EXECUTE_PROCESS(WORKING_DIRECTORY "${TA3D_SOURCE_DIR}/src/lua" COMMAND ${MAKE_WIN} mingw CC=${CMAKE_C_COMPILER} RANLIB=${RANLIB_WIN})
		SET( BUILT_LUA 1 CACHE BOOL "LUA has been built" )
	ENDIF( NOT BUILT_LUA )

	EXECUTE_PROCESS( COMMAND cp src/lua/lua51.dll ./ )

        IF( NOT BUILT_LUA )
	    EXECUTE_PROCESS( COMMAND make -C "src/lua/" clean)
	    EXECUTE_PROCESS( COMMAND make -C "src/lua/" mingw)
	    SET( BUILT_LUA 1 CACHE BOOL "LUA has been built")
	ENDIF( NOT BUILT_LUA )
	SET( LIBS "-L./lua -llua -luser32 -lgdi32 -lopengl32 -lglu32 -lz")
	IF(NOT CMAKE_CROSSCOMPILING)
		SET( LIBS "${LIBS} -lSDLmain")
	ENDIF(NOT CMAKE_CROSSCOMPILING)
	IF( MSVC )
#		Do we need something there ?
#	    	LINK_DIRECTORIES("${CMAKE_SOURCE_DIR}/src/tools/win32/msvc/static/")
	ELSE( MSVC )
	    	LINK_DIRECTORIES("${CMAKE_SOURCE_DIR}/src/tools/win32/mingw32/static/")
	ENDIF( MSVC )
ELSE( WIN32 OR CMAKE_CROSSCOMPILING )
    IF(NOT APPLE)
    	IF( NOT BUILT_LUA )
	    	EXECUTE_PROCESS( COMMAND make -C "src/lua/" clean )
	    	EXECUTE_PROCESS( COMMAND make -C "src/lua/" linux )
	    	SET( BUILT_LUA 1 CACHE BOOL "LUA has been built" )
	    ENDIF( NOT BUILT_LUA )
    ENDIF(NOT APPLE)

    SET(LIBS "-llua -lz")
    IF(NOT APPLE)
	    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -rdynamic -ffast-math")
    ENDIF(NOT APPLE)
ENDIF( WIN32 OR CMAKE_CROSSCOMPILING )

#
# --- GLEW ---
#
IF(WIN32 OR CMAKE_CROSSCOMPILING)
    SET(GLEW_LIB "-lglew32")
ELSE(WIN32 OR CMAKE_CROSSCOMPILING)
	find_library(GLEW_LIB GLEW)
	IF(NOT GLEW_LIB)
        find_library(GLEW_LIB glew)
	ENDIF(NOT GLEW_LIB)
ENDIF(WIN32 OR CMAKE_CROSSCOMPILING)
Message(STATUS "GLEW Library : ${GLEW_LIB}")
SET(LIBS "${LIBS} ${GLEW_LIB}")

#
# --- FTGL ---
#
IF(NOT APPLE)
	IF(WIN32 OR CMAKE_CROSSCOMPILING)
	    SET( FREETYPE_INCLUDE_DIRS "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/include/freetype2/")
	    SET( FREETYPE_LIBRARIES "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/libs/libfreetype.dll.a")
	    Message(STATUS "FreeType Library : ${FREETYPE_LIBRARIES}")
	ELSE(WIN32 OR CMAKE_CROSSCOMPILING)
	    FIND_PACKAGE(Freetype)
	ENDIF(WIN32 OR CMAKE_CROSSCOMPILING)
	include_directories( ${FREETYPE_INCLUDE_DIRS} )

	find_library(FTGL_LIB ftgl)
	find_path(FTGL_INCLUDE_lower NAMES FTGL/ftgl.h)

	IF(NOT FTGL_INCLUDE_lower AND NOT CMAKE_CROSSCOMPILING)
	    SET(__FTGL__lower__ false)
	ELSE(NOT FTGL_INCLUDE_lower AND NOT CMAKE_CROSSCOMPILING)
	    SET(__FTGL__lower__ true)
	ENDIF(NOT FTGL_INCLUDE_lower AND NOT CMAKE_CROSSCOMPILING)

	IF (WIN32 OR CMAKE_CROSSCOMPILING)
	    SET(__FTGL__lower__ true)
	    SET(FTGL_LIB "${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/libs/libftgl.a")
	ENDIF (WIN32 OR CMAKE_CROSSCOMPILING)

	Message(STATUS "FTGL Library : ${FTGL_LIB}")
ELSE(NOT APPLE)
	Execute_process(COMMAND pkg-config --libs  ftgl OUTPUT_VARIABLE FTGL_LIB OUTPUT_STRIP_TRAILING_WHITESPACE)
	Execute_process(COMMAND pkg-config --cflags  ftgl OUTPUT_VARIABLE FTGL_CFLAGS OUTPUT_STRIP_TRAILING_WHITESPACE)
	Message(STATUS "FTGL Library : ${FTGL_LIB}")
	Message(STATUS "FTGL CFLags : ${FTGL_CFLAGS}")
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${FTGL_CFLAGS}")
	SET(LIBS "${LIBS} ${FTGL_LIB}")
	SET(__FTGL__lower__ false)
ENDIF(NOT APPLE)

#
# --- ZLib ---
#
IF(WIN32)
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -I${TA3D_SOURCE_DIR}/src/tools/win32/mingw32/include/zlib")
ENDIF(WIN32)


SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${TA3D_SOURCE_DIR})
# for compatibility with old versions of cmake
SET(EXECUTABLE_OUTPUT_PATH ${TA3D_SOURCE_DIR})


#
# --- Installation process ---
#

IF(NOT WIN32 AND UNIX)
  message("-- TA3D will be installed in: ${CMAKE_INSTALL_PREFIX}")
ENDIF(NOT WIN32 AND UNIX)

SET(TA3D_RESOURCES_INSTALL_PATH "share/games/ta3d")
SET(TA3D_BINARIES_INSTALL_PATH "games")

install(DIRECTORY docs DESTINATION ${TA3D_RESOURCES_INSTALL_PATH} PATTERN ".*" EXCLUDE PATTERN "*~" EXCLUDE)
install(DIRECTORY gfx DESTINATION ${TA3D_RESOURCES_INSTALL_PATH} PATTERN ".*" EXCLUDE PATTERN "*~" EXCLUDE)
install(DIRECTORY gui DESTINATION ${TA3D_RESOURCES_INSTALL_PATH} PATTERN ".*" EXCLUDE PATTERN "*~" EXCLUDE)
install(DIRECTORY mods/ta3d DESTINATION ${TA3D_RESOURCES_INSTALL_PATH}/mods PATTERN ".*" EXCLUDE PATTERN "*~" EXCLUDE)
install(DIRECTORY objects3d DESTINATION ${TA3D_RESOURCES_INSTALL_PATH} PATTERN ".*" EXCLUDE PATTERN "*~" EXCLUDE)
install(DIRECTORY resources DESTINATION ${TA3D_RESOURCES_INSTALL_PATH} PATTERN ".*" EXCLUDE PATTERN "*~" EXCLUDE)
install(DIRECTORY scripts DESTINATION ${TA3D_RESOURCES_INSTALL_PATH} PATTERN ".*" EXCLUDE PATTERN "*~" EXCLUDE PATTERN "__campaign_script.lua" EXCLUDE)
install(DIRECTORY shaders DESTINATION ${TA3D_RESOURCES_INSTALL_PATH} PATTERN ".*" EXCLUDE PATTERN "*~" EXCLUDE)
install(DIRECTORY sky DESTINATION ${TA3D_RESOURCES_INSTALL_PATH} PATTERN ".*" EXCLUDE PATTERN "*~" EXCLUDE)

# copy the file that will add TA3D and 3DMEditor to the desktop menu
install(FILES distrib/linux/ta3d DESTINATION share/menu)
install(FILES distrib/linux/ta3d.desktop DESTINATION share/applications)
install(FILES ta3d.png DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})
install(FILES distrib/linux/3dmeditor.desktop DESTINATION share/applications)
install(FILES 3dmeditor.png DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})
install(FILES readme.html DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})
install(FILES readme_fr.html DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})
install(FILES TODO DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})
install(FILES AUTHORS DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})
install(FILES README DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})
install(FILES NEWS DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})
install(FILES ta3d.mod DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})
install(FILES ChangeLog DESTINATION ${TA3D_RESOURCES_INSTALL_PATH})

# Things are set to work after installation with packages (Linux only)

IF(NOT WIN32 AND NOT APPLE)
    IF(DEV)
        SET(TA3D_RESOURCES_PATH "${TA3D_SOURCE_DIR}/")
        SET(TA3D_OVERRIDE_PATHS false)
    ELSE(DEV)
        SET(TA3D_RESOURCES_PATH "/usr/${TA3D_RESOURCES_INSTALL_PATH}/")
        SET(TA3D_OVERRIDE_PATHS true)
    ENDIF(DEV)
ELSE(NOT WIN32 AND NOT APPLE)
    SET(TA3D_RESOURCES_PATH "${TA3D_SOURCE_DIR}/")
    SET(TA3D_OVERRIDE_PATHS false)
ENDIF(NOT WIN32 AND NOT APPLE)

#
# --- CPACK configuration ---
#

SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "TA3D (Total Annihilation 3D), RTS Engine")
SET(CPACK_PACKAGE_VENDOR "the TA3D team") 
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${TA3D_SOURCE_DIR}/README")
SET(CPACK_RESOURCE_FILE_LICENSE "${TA3D_SOURCE_DIR}/COPYING")
SET(CPACK_PACKAGE_INSTALL_DIRECTORY "${PACKAGE_STRING}")
SET(CPACK_GENERATOR "STGZ;DEB;RPM")
SET(CPACK_PACKAGE_CONTACT "${TA3D_MAIL_BUGREPORT}")
SET(CPACK_DEBIAN_PACKAGE_MAINTAINER "Roland Brochard")
SET(CPACK_DEBIAN_PACKAGE_SECTION "games")
SET(CPACK_DEBIAN_PACKAGE_DEPENDS "libsdl, zlib")

IF(NOT WIN32 AND UNIX)
  SET(CPACK_STRIP_FILES "ta3d" "3dmeditor")
  SET(CPACK_SOURCE_STRIP_FILES "")
ENDIF(NOT WIN32 AND UNIX)
  SET(CPACK_PACKAGE_EXECUTABLES "ta3d" "3dmeditor")
INCLUDE(CPack)


CONFIGURE_FILE( ${TA3D_SOURCE_DIR}/config.h.cmake ${TA3D_SOURCE_DIR}/config.h )

ADD_SUBDIRECTORY(src)
